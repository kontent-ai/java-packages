plugins {
    id "io.franzbecker.gradle-lombok" version "1.14"
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

dependencies {
    compile('com.squareup:javapoet:1.9.0')
    compile('com.google.guava:guava:23.0')
    compile project(':kontent-delivery')

    testCompile("junit:junit:4.12");
    testCompile("org.apache.httpcomponents:httpclient:4.5.3:tests");
    testCompile("org.apache.httpcomponents:httpclient:4.5.3");
}

lombok {
    version = "1.18.2"
    sha256 = ""
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

import io.franzbecker.gradle.lombok.task.DelombokTask
task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each { dir ->
        inputs.dir(dir)
        args(dir, "-d", outputDir)
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId = 'kontent-delivery-generators'
            groupId = repoGroupId
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Generators producing strongly-typed models based on Content Types in a Kentico Kontent project.')
                root.appendNode('name', 'Kontent model generator.')
                root.appendNode('url', 'https://kontent.ai')
                root.children().last() + pomConfig
            }
        }
    }
}

// TODO adjust before final release (do not forget userOrg set to Kentico + labels -> https://github.com/bintray/gradle-bintray-plugin#buildgradle)
bintray {
    user = bintrayUser
    key = bintrayKey
    publish = true
    publications = ['BintrayPublication']
    pkg {
        repo = 'kontent-java-monorepo'
        name = 'kontent-delivery-generators'
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Simply007/kontent-java-monorepo.git'
        version {
            name = version.name
            desc = 'Generators producing strongly-typed models based on Content Types in a Kentico Kontent project.'
            released = new Date()
            desc = version
            vcsTag = version
            gpg {
                sign = true
                passphrase = bintrayGpg
            }
        }
    }
}